plugins {
    id 'com.github.sherter.google-java-format' version "$googleJavaFormatPluginVersion"
    id 'name.remal.check-dependency-updates' version "$checkDependencyUpdateVersion"
}

allprojects {
    repositories {
        boolean localArtifactoryActive = System.getenv("ENABLE_LOCAL_ARTIFACTORY") != null ? Boolean.valueOf(System.getenv("ENABLE_LOCAL_ARTIFACTORY")) : false;
        if(localArtifactoryActive) {
            maven {
                url "${artifactoryLocalRepoUrl}"
                credentials {
                    username = "${artifactoryUser}"
                    password = "${artifactoryPassword}"
                }
            }
        } else {
            println "> localArtifactoryActive is NOT active. Please consider running gradle with i.e. ENABLE_LOCAL_ARTIFACTORY=true ./gradlew [COMMAND]"
        }

        mavenLocal()
        jcenter()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'name.remal.check-dependency-updates'

    // TODO: As I do not know exactly what I need yet, I am activating the preview feature for all subprojects and for
    //  the tests, run and compile targets.
    tasks.withType(JavaCompile).all {
        options.compilerArgs += ['--enable-preview']
    }
    tasks.withType(Test).all {
        jvmArgs += '--enable-preview'
    }
    tasks.withType(JavaExec) {
        jvmArgs += '--enable-preview'
    }

    if (name.equals('backend') || name.equals('definition-generator')) {
        apply plugin: 'com.github.sherter.google-java-format'

        googleJavaFormat {
            options style: 'AOSP'
        }
        verifyGoogleJavaFormat.dependsOn(tasks.googleJavaFormat)
    }

    if (name.contains('protobuf')) {
        dependencies {
            implementation group: 'io.grpc', name: 'grpc-netty-shaded', version: grpcVersion
            implementation group: 'io.grpc', name: 'grpc-protobuf', version: grpcVersion
            implementation group: 'io.grpc', name: 'grpc-stub', version: grpcVersion

            // necessary for Java 9+
            compileOnly group: 'org.apache.tomcat', name: 'annotations-api', version: apacheAnnotationsApi
        }
    }

    buildscript.repositories {
        mavenLocal()
        jcenter()
    }
}
